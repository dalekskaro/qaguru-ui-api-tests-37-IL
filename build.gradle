plugins {
    id 'java-library'
    id 'io.qameta.allure' version '3.0.0'
    id "io.freefair.lombok" version "9.0.0"
}

group = 'guru.qa.api'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

def restAssuredVersion = '5.5.2',
    junitJupiterVersion = '5.11.0',
    slf4jSimpleVersion = '2.0.17',
    jacksonVersion = '2.19.0',
    allureVersion = "2.21.0",
    assertjVersion = '3.27.3',
    fakerVersion = '1.0.2',
    selenideVersion = '7.9.3',
    allureSelenideVersion = '2.29.1',
    ownerVersion = '1.0.12'

allure {
    report {
        version.set(allureVersion)
    }
    adapter {
        aspectjWeaver.set(true)
        frameworks {
            junit5 {
                adapterVersion.set(allureVersion)
            }
        }
    }
}

dependencies {
    testImplementation(
            "io.rest-assured:rest-assured:$restAssuredVersion",
            "io.qameta.allure:allure-rest-assured:$allureVersion",
            "org.junit.jupiter:junit-jupiter:$junitJupiterVersion",
            "org.slf4j:slf4j-simple:$slf4jSimpleVersion",
            "com.fasterxml.jackson.core:jackson-core:$jacksonVersion",
            "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion",
            "org.assertj:assertj-core:$assertjVersion",
            "com.github.javafaker:javafaker:$fakerVersion",
            "com.codeborne:selenide:$selenideVersion",
            "io.qameta.allure:allure-selenide:$allureSelenideVersion",
            "org.aeonbits.owner:owner:$ownerVersion"
    )
}

tasks.withType(Test) {
    systemProperties System.getProperties()
    useJUnitPlatform()

    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}

tasks.register('apiTest', Test) {
    systemProperties(System.getProperties())
    useJUnitPlatform {
        includeTags("api")
    }
}